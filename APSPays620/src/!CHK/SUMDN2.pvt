0010 ! 0 REBUILD MDINST, AS IT MAY BE IN THE WRONG FORMAT <SUMDN2>
0032 ! COPYRIGHT 1996-99 BY DSD BUSINESS SYSTEMS
0033 ! WRITTEN FOR THE EXTENDED MASTER DEVELOPER UTILITIES - EMD300
0040 ! ORIG: DSD 09/15/96 MOD 07/29/97 (FOR UNIX)
0050 ! 
0060 ! THIS PROGRAM IS PERFORMED IN SUMDIN
0070 ! 
0080 ! IF A FILE NAMED "MDINST" EXISTS IN THE "SOA" DIRECTORY AND THERE IS
0090 ! A LINE IN THAT FILE THAT BEGINS WITH "MD" THEN THAT FILE IS BLOWN AWAY
0100 ! AND A CORRECT REPLACEMENT FILE IS CREATED.
0110 ! 
0120 ! THE REASON FOR THIS IS THAT MORE THAN ONE DSD ENHANCEMENT MAY BE
0130 ! INSTALLED AT A TIME AND THE MDINST FILE WOULD BE OVERWRITTEN BY SECOND
0140 ! AND SUBSEQUENT INSTALLATIONS FROM DISK. FOR THIS REASON, WE SCAN
0150 ! ALL THE DIRECTORIES IN ..\MAS90 AND LOOK FOR ENHANCEMENTS THAT NEED TO
0160 ! BE INSTALLED AND THEN CREATE A GOOD MDINST, ON THE FLY. THEN SUMDIN
0170 ! USES MDINST TO DO ITS THING.
0180 ! 
0190 ! **********************************************************************
0200 ! 
0210 IF NOT(%SYS_GUIPROG) THEN PRINT @(22,1),"SCANNING MAS90 DIRECTORIES ..."
0220 GOSUB REBUILD_MDINST
0230 GOTO 9900
0240 ! 
0250 ! **********************************************************************
0260 REBUILD_MDINST:
0270 SETESC REBUILD_MDINST_EXIT; SETERR REBUILD_MDINST_EXIT
0280 IF %SYS_GUIPROG THEN WAIT .5
0290 CTLCHN=6 ! SUMDIN OPENS SY0CTL.SOA ON CHN 6
0300 ! 
0310 ! ----------------------------------------------------------------------
0320 ! OPEN FILES
0330 ! 
0340 DIR$="SOA"; CALL "SYPATH",TERM$,DIR$; IF DIR$="ERR" THEN GOTO REBUILD_MDINST_EXIT
0350 MDINSTCHN=UNT,E1=MDINSTCHN,E1$="MDINST"; OPEN (E1,ERR=0450)E1$
0360 ! 
0370 ! ----------------------------------------------------------------------
0380 ! CHECK TO SEE WHETHER THIS IS AN MDINST FILE, WHICH IS DEFINED AS A
0390 ! TEXT FILE WITH A LINE THAT STARTS WITH "MD"
0400 ! 
0410 ! READ (E1,END=REBUILD_MDINST_EXIT)MDINST$
0420 ! IF LEN(MDINST$)<2 THEN GOTO 0410
0430 ! IF MDINST$(1,2)<>"MD" THEN GOTO 0410
0450 MDINST$=$$
0460 ! 
0470 ! ----------------------------------------------------------------------
0480 ! REBUILD IS NECESSARY - LOOK FOR ALL INSTALLED ENHANCEMENTS
0490 ! 
0500 DIR$="SOA"; CALL "SYPATH",TERM$,DIR$ ! THERE A SOA DIRECTORY?
0510 IF DIR$="ERR" THEN EXIT ! IF NO SOA DIR, EXIT
0520 ! 
0530 ! CRUISE THROUGH SY0FIL.SOA AND FIND LEVEL RECORDS.
0540 ! USE LEVEL RECORDS TO ASCERTAIN WHICH MAS90 MODULES ARE PRESENT.
0550 ! 
0555 IF %SYS_SS THEN GOTO SY_MODULE_LOOP
0560 E1=CTLCHN,E2$="l"; READ (E1,KEY=E2$,DOM=0590) ! GO 1ST "l" REC
0570 ! 
0580 TOP_L_LOOP: ! TOP OF L RECORD LOOP
0590 E1=CTLCHN,E2$=KEY(E1,END=WRITE_MDINST); READ (E1,KEY=E2$)APPLN$
0600 IF APPLN$(1,1)<>"l" THEN GOTO WRITE_MDINST ! AFTER LAST LEV REC... DONE
0610 IF LEN(E2$)<>4 THEN GOTO TOP_L_LOOP ! REJECT IF NOT COREECT KEY LENGTH
0620 ! SET PROGRAM DIRECTORY
0630 IF E2$(2,3)="SYS" THEN PROG_DIR$="SOA" ELSE PROG_DIR$=E2$(2,1)+E2$(4,1)
0640 ! TRY TO CHDIR TO PROGRAM DIRECTORY
0650 DIR$=PROG_DIR$; CALL "SYPATH",TERM$,DIR$
0660 ! IF CAN'T CHDIR TO PROG DIR THEN REJECT THIS & GET ANOTHER LEVEL RECORD
0670 IF DIR$="ERR" THEN GOTO TOP_L_LOOP
0680 ! 
0690 ! OPEN THE PROGRAM DIRECTORY AS A FILE
0700 CWDIR "..",ERR=TOP_L_LOOP
0710 E1=UNT,DIRCHN=E1,E1$=DIR$; OPEN (E1,ERR=TOP_L_LOOP)E1$
0720 ! SET THE NAME OF THE FILE WE'RE SEARCHING FOR
0730 IF PROG_DIR$="SOA" THEN SEARCH_FILENAME$="IN0CTL." ELSE SEARCH_FILENAME$=PROG_DIR$+"0CTL."; DIR$=PROG_DIR$; CALL "SYPATH",TERM$,DIR$
0740 TMP$=UCS(SYS); IF POS("UNIX"=TMP$) THEN SEARCH_FILENAME$=LCS(SEARCH_FILENAME$)
0750 ! 
0760 TOP_F_LOOP: ! TOP OF THE FILENAME SEARCH LOOP
0770 E1=DIRCHN; READ RECORD (E1,END=TOP_L_LOOP)FILENAME$
0780 ! 
0790 ! REJECT DOT AND DOT-DOT ENTRIES
0800 IF FILENAME$="." OR FILENAME$=".." OR LEN(FILENAME$)<10 THEN GOTO TOP_F_LOOP
0810 IF FILENAME$(1,7)<>SEARCH_FILENAME$ OR FILENAME$(8,3)="SOA" OR FILENAME$(8,3)="soa" THEN GOTO TOP_F_LOOP
0820 E1=UNT,E1$=FILENAME$; CLOSE (E1); OPEN (E1,ERR=TOP_F_LOOP)E1$
0830 READ (E1,KEY="l",DOM=*NEXT,END=BOTTOM_F_LOOP)
0840 READ (E1,END=BOTTOM_F_LOOP)LEVL$
0850 ! 
0860 ! A CORRECT FILENAME WAS FOUND, ADD A RECORD TO MDINST$
0870 ! NOT THAT MDINST$ IS THE STRING THAT WILL BE THE CONTENTS OF "MDINST"
0880 IF PROG_DIR$="SOA" OR PROG_DIR$="soa" THEN MDINST$=MDINST$+"IN" ELSE MDINST$=MDINST$+PROG_DIR$
0890 MDINST$=MDINST$+FILENAME$(8,3)+LEVL$(19,27)+SEP
0900 ! 
0910 BOTTOM_F_LOOP: CLOSE (E1); GOTO TOP_F_LOOP
0920 ! 
0930 ! **********************************************************************
0940 WRITE_MDINST:
0950 ! 
0960 ! WRITE MDINST RECORDS TO "MDINST" FILE
0970 CLOSE (MDINSTCHN); CLOSE (DIRCHN)
0980 DIR$="SOA"; CALL "SYPATH",TERM$,DIR$; IF DIR$="ERR" THEN GOTO REBUILD_MDINST_EXIT
0990 E1$="MDINST"; ERASE E1$,ERR=*NEXT
1000 IF MDINST$=$$ THEN GOTO REBUILD_MDINST_EXIT ELSE SERIAL "MDINST",ERR=REBUILD_MDINST_EXIT
1010 E1$="MDINST"; OPEN (MDINSTCHN)E1$
1020 LOCK (MDINSTCHN)
1030 PRINT (MDINSTCHN)MDINST$,
1040 ! 
1050 ! **********************************************************************
1060 REBUILD_MDINST_EXIT:
1070 IF MDINSTCHN THEN CLOSE (MDINSTCHN)
1080 ! IN GUI MODE, WE ISSUE THIS METACOMMAND TO NOMADS TO DESTROY WINDOW:
1090 IF %SYS_GUIPROG AND NOT(%SYS_SS) THEN CMD_STR$="END"
1100 RETURN 
1110 ! 
1120 SY_MODULE_LOOP:
1130 ! 
1140 CTLCHN=%SYS_SS'OPENTABLE("SY_Module","SYSTEM")
1141 TOP_MODULE_LOOP:
1150 E1=CTLCHN,E2$=KEY(E1,END=WRITE_MDINST); READ (E1,KEY=E2$)
1160 IF E2$(1,3)="SYS" THEN PROG_DIR$="SOA" ELSE PROG_DIR$=E2$(1,1)+E2$(3,1)
1170 DIR$=PROG_DIR$; CALL "SYPATH",TERM$,DIR$
1180 ! IF CAN'T CHDIR TO PROG DIR THEN REJECT THIS & GET ANOTHER LEVEL RECORD
1190 IF DIR$="ERR" THEN GOTO TOP_MODULE_LOOP
1200 ! OPEN THE PROGRAM DIRECTORY AS A FILE
1210 CWDIR "..",ERR=TOP_MODULE_LOOP
1220 E1=UNT,DIRCHN=E1,E1$=DIR$; OPEN (E1,ERR=TOP_MODULE_LOOP)E1$
1230 ! SET THE NAME OF THE FILE WE'RE SEARCHING FOR
1240 IF PROG_DIR$="SOA" THEN SEARCH_FILENAME$="IN0CTL." ELSE SEARCH_FILENAME$=PROG_DIR$+"0CTL."; DIR$=PROG_DIR$; CALL "SYPATH",TERM$,DIR$
1260 ! 
1270 TOP_FILE_LOOP: ! TOP OF THE FILENAME SEARCH LOOP
1280 E1=DIRCHN; READ RECORD (E1,END=TOP_MODULE_LOOP)FILENAME$
1290 ! 
1300 ! REJECT DOT AND DOT-DOT ENTRIES
1310 IF FILENAME$="." OR FILENAME$=".." OR LEN(FILENAME$)<10 THEN GOTO TOP_FILE_LOOP
1320 IF FILENAME$(1,7)<>SEARCH_FILENAME$ OR FILENAME$(8,3)="SOA" OR FILENAME$(8,3)="soa" THEN GOTO TOP_FILE_LOOP
1330 E1=UNT,E1$=FILENAME$; CLOSE (E1); OPEN (E1,ERR=TOP_FILE_LOOP)E1$
1340 READ (E1,KEY="l",DOM=*NEXT,END=BOTTOM_FILE_LOOP)
1350 READ (E1,END=BOTTOM_FILE_LOOP)LEVL$
1360 ! 
1370 ! A CORRECT FILENAME WAS FOUND, ADD A RECORD TO MDINST$
1380 ! NOT THAT MDINST$ IS THE STRING THAT WILL BE THE CONTENTS OF "MDINST"
1390 IF PROG_DIR$="SOA" OR PROG_DIR$="soa" THEN MDINST$=MDINST$+"IN" ELSE MDINST$=MDINST$+PROG_DIR$
1400 MDINST$=MDINST$+FILENAME$(8,3)+LEVL$(19,27)+SEP
1410 ! 
1420 BOTTOM_FILE_LOOP: CLOSE (E1); GOTO TOP_FILE_LOOP
9900 EXIT_PROG:
9910 ! 
9920 EXIT 
