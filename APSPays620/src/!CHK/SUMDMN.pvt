0001 REM 0
0010 ! ! EXTENDED M/D UTILITIES MENU INSERTION PUBLIC PROGRAM <SUMDMN>
0030 ! ! COPYRIGHT (c) 1996 BY DSD ASSOCIATES, INC.
0031 ! ! WRITTEN FOR THE EXTENDED MASTER DEVELOPER UTILITIES - EMD300
0032 ! ! ORIG: DSD 09/15/96
0040 ! ! fix for bad Menu Header string length (12/05/01 RLB)
0050 ! !
0070 ! ! SUBSTRING (4,1) OF THE INPUT FILE IS THE LINE TYPE, DESCRIBED BELOW:
0080 ! !  LINE TYPE "A" = ADD NEW MENU SELECTION TO EXISTING MENU
0090 ! !  LINE TYPE "M" = ADD NEW MENU TO EXISTING APPLICATION
0100 ! !  LINE TYPE "B" = NEW MENU BAR NAME (USED TO COMPRESS AN EXISTING)
0110 ! !  LINE TYPE "D" = DELETE AN EXISTING MENU SELECTION
0120 ! !  LINE TYPE "X" = DELETE AN EXISTING MENU IN ITS ENTIRETY
0130 ! **********************************************************************
0140 ! 
0150 SETERR SUMDMN_EXIT; SETESC SUMDMN_EXIT
0151 ENTER INP_FILE$,PATH$,TERM$
0170 ! 
0180 ! **********************************************************************
0190 ! OPEN FILES
0200 ! 
0210 CWDIR PATH$,ERR=SUMDMN_EXIT
0230 ! 
0240 INP_CHAN_VAL=UNT; OPEN (INP_CHAN_VAL,ERR=SUMDMN_EXIT)INP_FILE$
0250 DIR$="SOA"; CALL "SYPATH",TERM$,DIR$
0260 IF DIR$="ERR" THEN GOTO SUMDMN_EXIT
0270 ! 
0280 MNU_CHAN_VAL=UNT; OPEN (MNU_CHAN_VAL,ERR=SUMDMN_EXIT)"SY0MNU.SOA"
0290 ! 
0300 ! **********************************************************************
0310 ! READ NEXT MENU RECORD
0320 ! 
0330 READ (MNU_CHAN_VAL,KEY="",DOM=TOP_MR_LOOP)
0340 ! 
0350 TOP_MR_LOOP: ! TOP OF MENU RECORD LOOP <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
0360 ! 
0370 E2$=KEY(INP_CHAN_VAL,END=SUMDMN_EXIT)
0380 READ (INP_CHAN_VAL,KEY=E2$)MNU$
0390 MNU$=MNU$+DIM(65),MNU$=MNU$(1,65)
0400 IF MNU$(5,2)="LM" THEN APPLN$="S/A" ELSE APPLN$=MNU$(5,1)+"/"+MNU$(6,1)
0410 MENU_CODE$=MNU$(7,3)+DIM(3)
0420 ! BRANCH TO APPROPRIATE ROUTINES
0430 ON POS(MNU$(4,1)="AMBDX") GOTO BOTTOM_MR_LOOP,0455,1255,1645,1895,2225,BOTTOM_MR_LOOP
0440 ! 
0450 ! **********************************************************************
0460 ! ADD A MENU SELECTION TO AN EXISTING MENU - FIRST READ MENU RECORD
0470 ! 
0480 E2$=APPLN$+"B"+MNU$(7,3),E2$=E2$+DIM(10),E2$=E2$(1,10),MENU$=""
0490 READ (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)OPTIONS$,COLS,ROWS,OP
0500 ! 
0510 ! **********************************************************************
0520 ! READ REMAINING MENU RECORDS (IF ADD'L MENUS EXIST)
0530 ! 
0540 MENU$=MENU$+OPTIONS$(40,600),E2$(4,1)=CHR(ASC(E2$(4,1))+1)
0550 READ (MNU_CHAN_VAL,KEY=E2$,DOM=0575)OPTIONS$,COLS,ROWS,OP; GOTO 0540
0560 ! 
0570 ! **********************************************************************
0580 ! IS THE MENU ITEM ALREADY THERE?  IF SO, DON'T ADD A NEW MENU SELEC-
0590 ! TION, JUST UPDATE THE ONE THAT'S THERE.  PROG NAME IS THE KEY.
0600 ! 
0610 POSN=POS(MNU$(12,6)=MENU$,40); IF POSN<1 THEN GOTO 0645
0620 GOTO 0975
0630 ! 
0640 ! **********************************************************************
0650 ! MUST WE MAKE SPACE? CHECK TO SEE IF THE LAST MENU OPTION IS USED.
0660 ! IF SO, THEN ATTEMPT TO COMPRESS OUT ANY EXISTING BLANK LINES IN
0670 ! THE MENU, IF THERE ARE ANY, BEGINNING WITH THE FIRST ONE.  IF NO
0680 ! BLANKS, THEN ADD A NEW BLANK MENU (SELECTIONS 16-30, ETC.)
0690 ! 
0700 IF MENU$(LEN(MENU$)-39,40)=DIM(40) THEN GOTO 0765
0710 FOR I=LEN(MENU$)-39 TO 1 STEP -40
0720 IF MENU$(I,40)=DIM(40) THEN MENU$=MENU$(1,I-1)+MENU$(I+40)+DIM(40); EXITTO 0765
0730 NEXT I
0740 MENU$=MENU$+DIM(600)
0750 ! 
0760 ! **********************************************************************
0770 ! PLACE SELECTION ON MENU.  IF SELECTION NUMBER IS "00", THEN PUT
0780 ! IT ON THE END.
0790 ! 
0800 IF MNU$(10,2)<>"00" THEN GOTO 0895
0810 ! 
0820 FOR I=LEN(MENU$)-39 TO 1 STEP -40
0830 IF MENU$(I,40)<>DIM(40) THEN EXITTO 0975
0840 POSN=I
0850 NEXT I
0860 ! 
0870 GOTO 0975
0880 ! 
0890 ! **********************************************************************
0900 ! SELECTION NUMBER IS NOT "00" - INSERT A BLANK SELECTION AT THE
0910 ! DESIRED LOCATION.
0920 ! 
0930 POSN=40*(NUM(MNU$(10,2),ERR=0975)-1)+1
0940 MENU$=MENU$(1,POSN-1)+DIM(40)+MENU$(POSN)
0950 MENU$=MENU$(1,LEN(MENU$)-40)
0960 ! 
0970 ! **********************************************************************
0980 ! POSN CONTAINS A BLANK - PUT THE SELECTION THERE.  IF POSN DOES NOT
0990 ! POINT TO A BLANK, THAT MEANS THE MENU SELECTION ALREADY EXISTED
1000 ! AND WE'RE MERELY UPDATING IT.
1010 ! 
1020 MENU$(POSN,40)=MNU$(12,6)+MNU$(18,34)
1030 IMAGE$="",ROWS=0,OP=0,OPTIONS=0
1040 KEY$=APPLN$+"B"+MNU$(7,3),KEY$=KEY$+DIM(10),KEY$=KEY$(1,10)
1050 DIM OPTIONS$(1240); OPTIONS$(1)=KEY$,OPTIONS$(40,600)=MENU$(1,600)
1060 ! 
1070 FOR LOOP=1 TO 15
1080 IF POS(" "<>MENU$(LOOP*40-39))=0 THEN EXITTO 1150
1090 ROWS=ROWS+1
1100 IF DIM(40)=MENU$(LOOP*40-39,40) THEN IMAGE$=IMAGE$+DIM(40); GOTO 1130
1110 OP=OP+1,OPTIONS=OPTIONS+1
1120 IMAGE$=IMAGE$+STR(OP:" ##.  ")+MENU$(LOOP*40-33,34)
1130 NEXT LOOP
1140 ! 
1150 OPTIONS$(640)=IMAGE$,ROWS=ROWS+4
1160 EXTRACT (MNU_CHAN_VAL,KEY=KEY$,DOM=1170,ERR=BOTTOM_MR_LOOP)
1170 WRITE (MNU_CHAN_VAL,KEY=KEY$)OPTIONS$,COLS,ROWS,OPTIONS
1180 OPTIONS=0,ROWS=0,KEY$(4,1)=CHR(ASC(KEY$(4,1))+1),IMAGE$=""
1190 IF LEN(MENU$)<=600 THEN GOTO BOTTOM_MR_LOOP
1200 MENU$=MENU$(601)
1210 IF LEN(MENU$)<600 THEN MENU$=MENU$+DIM(600-LEN(MENU$))
1220 IF STP(MENU$,1)="" THEN GOTO BOTTOM_MR_LOOP
1230 GOTO 1050
1240 ! 
1250 ! **********************************************************************
1260 ! ADD A NEW MENU BAR SELECTION (& BLANK MENU) TO AN EXISTING APPL'N
1270 ! 
1280 E2$=APPLN$+"AHEADER"; READ (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)OPTIONS$,BARS
1290 ! 
1300 ! **********************************************************************
1310 ! INSERT THE MENU INFORMATION
1320 ! 
1330 IF OPTIONS$(490,1)<>" " THEN GOTO BOTTOM_MR_LOOP ! NO ROOM FOR NEW MENU!
1340 ! 
1350 ! **********************************************************************
1360 ! CHECK FOR EXISTING MENU BAR OPTION CODE
1370 ! 
1380 FOR LOOP=1 TO 10
1390 IF OPTIONS$(40+(LOOP-1)*50,6)=MENU_CODE$ THEN EXITTO BOTTOM_MR_LOOP
1400 NEXT LOOP
1410 ! 
1420 ! **********************************************************************
1430 ! ADD THE NEW SELECTION TO THE STRING PORTION
1440 ! 
1450 NEW_OPTIONS$=OPTIONS$(1,39+(NUM(MNU$(10,2))-1)*50)+MENU_CODE$+MNU$(52,14)+MNU$(18,30)+OPTIONS$(40+(NUM(MNU$(10,2))-1)*50),OPTIONS$(1,539)=NEW_OPTIONS$
1460 ! 
1470 ! **********************************************************************
1480 ! ADD THE NEW SELECTION TO THE MENU BAR
1490 ! 
1500 MENU$=OPTIONS$(1,539); GOSUB BUILD_BAR_IMAGE
1510 OPTIONS$=PAD(OPTIONS$,699),OPTIONS$(540,160)=MENU_BAR$ ! fix bad menu header record (12/05/01 RLB)
1520 E2$=OPTIONS$(1,10); EXTRACT (MNU_CHAN_VAL,KEY=E2$,DOM=1555,ERR=BOTTOM_MR_LOOP)
1530 E2$=OPTIONS$(1,10); WRITE (MNU_CHAN_VAL,KEY=E2$)OPTIONS$,BARS
1540 ! 
1550 ! **********************************************************************
1560 ! AND FINALLY, CREATE A NEW MENU RECORD IN SY0MNU
1570 ! 
1580 DIM OPTIONS$(1240)
1590 OPTIONS$(1)=APPLN$+"B"+MNU$(7,3),COLS=42,ROWS=4,OP=0
1600 E2$=OPTIONS$(1,10); EXTRACT (MNU_CHAN_VAL,KEY=E2$,DOM=1610,ERR=BOTTOM_MR_LOOP)
1610 E2$=OPTIONS$(1,10); WRITE (MNU_CHAN_VAL,KEY=E2$)OPTIONS$,COLS,ROWS,OP
1620 GOTO BOTTOM_MR_LOOP
1630 ! 
1640 ! **********************************************************************
1650 ! CHANGE THE NAME OF AN EXISTING MENU BAR SELECTION
1660 ! 
1670 E2$=APPLN$+"AHEADER"; READ (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)OPTIONS$,BARS
1680 ! 
1690 ! **********************************************************************
1700 ! CHECK FOR EXISTING MENU BAR OPTION CODE
1710 ! 
1720 MENU$=OPTIONS$(40,500)
1730 ! 
1740 FOR LOOP=1 TO 10
1750 IF MENU$((LOOP-1)*50+1,6)=MENU_CODE$ THEN EXITTO 1805
1760 NEXT LOOP
1770 ! 
1780 GOTO BOTTOM_MR_LOOP
1790 ! 
1800 ! **********************************************************************
1810 ! CHANGE THE MENU BAR NAME
1820 ! 
1830 OLD_DESCR$=STP(MENU$((LOOP-1)*50+7,14),1),MENU$((LOOP-1)*50+7,14)=MNU$(52,14),OPTIONS$(1,539)=MENU$; GOSUB BUILD_BAR_IMAGE
1840 OPTIONS$(540,160)=MENU_BAR$
1850 E2$=OPTIONS$(1,10); EXTRACT (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP,ERR=2505)
1860 E2$=OPTIONS$(1,10); WRITE (MNU_CHAN_VAL,KEY=E2$)OPTIONS$,BARS
1870 GOTO 2200
1880 ! 
1890 ! **********************************************************************
1900 ! DELETE AN EXISTING MENU SELECTION FROM A MENU
1910 ! 
1920 E2$=APPLN$+"B"+MNU$(7,3),E2$=E2$+DIM(10),E2$=E2$(1,10),MENU$=""
1930 READ (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)OPTIONS$,COLS,ROWS,OP
1940 ! **********************************************************************
1950 ! READ REMAINING MENU RECORDS (IF THEY EXIST)
1960 ! 
1970 MENU$=MENU$+OPTIONS$(40,600),E2$(4,1)=CHR(ASC(E2$(4,1))+1)
1980 READ (MNU_CHAN_VAL,KEY=E2$,DOM=1995)OPTIONS$,COLS,ROWS,OP; GOTO 1970
1990 ! **********************************************************************
2000 ! LOCATE THE MENU ITEM
2010 ! 
2020 POSN=POS(MNU$(12,6)=MENU$,40); IF POSN=0 THEN GOTO 2200
2030 E2$=APPLN$+"B"+MNU$(7,3),E2$=E2$+DIM(10),E2$=E2$(1,10),MENU$=""
2040 E2$(4,1)=CHR(ASC("B")+INT((POSN-1)/600))
2050 READ (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)OPTIONS$,COLS,ROWS,OP
2060 MENU$=OPTIONS$(40,600),POSN=POS(MNU$(12,6)=MENU$,40); IF POSN=0 THEN GOTO 2200
2070 ! **********************************************************************
2080 ! DELETE THE MENU ITEM
2090 ! 
2100 MENU$=MENU$(1,POSN-1)+MENU$(POSN+40)+DIM(40),OPTIONS$(40,600)=MENU$
2110 IMAGE$=OPTIONS$(640),IMAGE$=IMAGE$(1,POSN-1)+IMAGE$(POSN+40)+DIM(40)
2120 ! **********************************************************************
2130 ! RENUMBER THE REMAINING MENU ITEMS
2140 ! 
2150 MENU_OP=1
2160 FOR LOOP=1 TO 15; IF IMAGE$((LOOP-1)*40+1,40)<>DIM(40) THEN IMAGE$((LOOP-1)*40+1,6)=STR(MENU_OP:" ##.  "),MENU_OP=MENU_OP+1 END_IF ; NEXT LOOP
2170 OPTIONS$(640)=IMAGE$
2180 E2$=OPTIONS$(1,10); EXTRACT (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP,ERR=BOTTOM_MR_LOOP)
2190 E2$=OPTIONS$(1,10); IF OPTIONS$(640,600)=DIM(600) THEN REMOVE (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP) ELSE WRITE (MNU_CHAN_VAL,KEY=E2$)OPTIONS$,COLS,ROWS-1,OP-1
2200 GOTO TOP_MR_LOOP
2210 ! **********************************************************************
2220 DELETE_AN_ENTIRE_MENU:
2230 ! DELETE AN EXISTING MENU IN ITS ENTIRETY
2240 ! 
2250 E2$=APPLN$+"AHEADER"; READ (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)OPTIONS$,BARS
2260 ! **********************************************************************
2270 ! CHECK FOR EXISTING MENU BAR OPTION CODE
2280 ! 
2290 MENU$=OPTIONS$(40,500)
2300 FOR LOOP=1 TO 10; IF MENU$((LOOP-1)*50+1,6)=MENU_CODE$ THEN EXITTO 2335
2310 NEXT LOOP
2320 GOTO BOTTOM_MR_LOOP
2330 ! **********************************************************************
2340 ! DELETE THE MENU FROM THE HEADER RECORD
2350 ! 
2360 MENU$((LOOP-1)*50+1)=MENU$(LOOP*50+1)
2370 OPTIONS$(40,500)=MENU$,MENU$=OPTIONS$(1,539)
2380 GOSUB BUILD_BAR_IMAGE
2390 OPTIONS$(540,160)=MENU_BAR$
2400 E2$=OPTIONS$(1,10); EXTRACT (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP,ERR=BOTTOM_MR_LOOP)
2410 E2$=OPTIONS$(1,10); WRITE (MNU_CHAN_VAL,KEY=E2$)OPTIONS$,BARS
2420 ! **********************************************************************
2430 DELETE_MENU_RECORD:
2440 ! 
2450 E2$=APPLN$+"B"+MENU_CODE$; REMOVE (MNU_CHAN_VAL,KEY=E2$,DOM=BOTTOM_MR_LOOP)
2460 E2$=APPLN$+CHR(ASC(E2$(4,1))+1)+MENU_CODE$
2470 GOTO DELETE_MENU_RECORD
2480 ! 
2490 GOTO TOP_MR_LOOP
2500 ! **********************************************************************
2510 ! DONE WITH THE LINE - GET THE NEXT RECORD FROM XX0MNU
2520 ! 
2530 BOTTOM_MR_LOOP: ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
2540 GOTO TOP_MR_LOOP
2550 ! **********************************************************************
2560 ! DONE
2570 ! 
2571 ! **********************************************************************
2580 SUMDMN_EXIT:
2590 IF MNU_CHAN_VAL THEN CLOSE (MNU_CHAN_VAL)
2600 IF INP_CHAN_VAL THEN CLOSE (INP_CHAN_VAL)
2610 EXIT 
2620 ! 
2630 ! **********************************************************************
2640 ! SUBROUTINES
2650 ! **********************************************************************
2660 BUILD_BAR_IMAGE:
2670 ! 
2680 MENU_BAR$="|",BARS=2,BAR_ADDITIONS$=" DATE "+"|"+" END "+"|"
2690 IF MENU$(1,3)="S/A" THEN BARS=3,BAR_ADDITIONS$=" COMPANY "+"|"+BAR_ADDITIONS$
2700 ! 
2710 FOR LOOP=1 TO 10
2720 IF POS(" "<>MENU$(50*(LOOP-1)+40,50*(11-LOOP)))<>0 THEN MENU_BAR$=MENU_BAR$+" "+MENU$(50*(LOOP-1)+46,POS(" "<>MENU$(50*(LOOP-1)+46,14),-1))+" "+"|",BARS=BARS+1
2730 NEXT LOOP
2740 ! 
2750 IF LEN(MENU_BAR$)>160-LEN(BAR_ADDITIONS$) THEN MENU_BAR$=MENU_BAR$(1,160-LEN(BAR_ADDITIONS$)-2)+" "+"|"
2760 MENU_BAR$=MENU_BAR$+BAR_ADDITIONS$
2770 IF LEN(MENU_BAR$)>80 THEN MENU_BAR$=MENU_BAR$(1,POS("|"=MENU_BAR$(1,80),-1))+DIM(80-POS("|"=MENU_BAR$(1,80),-1))+"|"+MENU_BAR$(POS("|"=MENU_BAR$(1,80),-1)+1)
2780 IF LEN(MENU_BAR$)<=160 THEN GOTO 2800 ELSE MENU_BAR$=MENU_BAR$(1,LEN(MENU_BAR$)-LEN(BAR_ADDITIONS$)-2)+MENU_BAR$(LEN(MENU_BAR$)-LEN(BAR_ADDITIONS$)); GOTO 2780
2790 ! 
2800 NUM_BARS=0
2810 FOR LOOP=2 TO LEN(MENU_BAR$)
2820 IF MENU_BAR$(LOOP,1)="|" THEN NUM_BARS=NUM_BARS+1
2830 NEXT LOOP
2840 ! 
2850 IF NUM_BARS<BARS THEN BARS=NUM_BARS
2860 RETURN 
