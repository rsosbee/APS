! IN_Installation class definition Template
! Created 06/28/04 By DSD Business Systems
! This template is used for 4.0 Installation
! Rename to the Primary Module (i.e. AP_Installation.pvc for APCB.400)
! for APS all versions. Updated 2020-08-16 RCS
! 
DEF CLASS "IN_Installation"
	LIKE "SY_Installation"
	FUNCTION POSTINSTALL()POST_INSTALL
	FUNCTION PREINSTALL()PRE_INSTALL
	FUNCTION PREFINALIZE()PRE_FINALIZE
	FUNCTION POSTFINALIZE()POST_FINALIZE
	FUNCTION POSTUNINSTALL()POST_UNINSTALL
	FUNCTION PREUNINSTALL()PRE_UNINSTALL
END DEF
!

ON_CREATE:
	! Required to function, set TERM$ for SUMDI4
	ENTER COSESSION
RETURN 
!

POST_INSTALL:
	! Run after the files have been copied from xxINST in MAS90
	! This should be called to run SUMDI4, after any new Files have been placed in xxINST
	! These are the only calls required to install
	TERM$		= COSESSION'LEGACYTERM$
	TEMPOBJ		= NEW("*dict/database")
	TEMPOBJ2	= NEW("*dict/database")
	CALL "..\SOA\SUMDI4",ERR=*NEXT,CMP_STRING$,TERM$,TEMPOBJ,TEMPOBJ2
	IF NOT(NUL(CMP_STRING$)) {
		FOR COMPILE=1 TO (LEN(CMP_STRING$)-1)
			APP$=CMP_STRING$(COMPILE,2),%RECOMPILE_FLG=1,V$=APP$(1,1)+"/"+APP$(2,1) ! for speed.
			TMP=_OBJ'COMPILEDICTIONARY(V$)
		NEXT COMPILE
	}
	! Compile menus.
	oMenus			= NEW("SY_Menu_svc",coSession,ERR=*NEXT)
	skipProgress	= isTRUE
	tmpRetVal		= oMenus'CompileMenu(skipProgress)
	DROP OBJECT oMenus,ERR=*NEXT
	oMenus			= 0

RETURN 
!

PRE_INSTALL:
	! 
	! This hook is called from SY_Installation'Install before any files are merged from xxINST
	! 
	! See if there is a Module to be installed
	IF MID(PGN,1,2)	= "IN" { RETURN  }
	IF POS(MID(PGN,1,2)="RS|CX|TR|EM|SM|IG|CF|MB|") {	! 08/18/2011 (JCR) 11/11/2011 (JCR) 09/31/2015 (JCR) 1/21/16 (JGD)
	  DIR$			= MID(PGN,1,2)
	  CWDIR "..\"+DIR$
	  SELECT MODULE_FILE$ FROM LWD WHERE POS("_MODULE"=UCS(MODULE_FILE$))
		  GOSUB INSTALL_MODULE_FILE	! *SC-OK*
	  NEXT RECORD
	}

RETURN 
! 

PRE_FINALIZE:
	! 
	! This hook is called from SY_Installation after mergeing, but before compiling menus
RETURN 
! 

POST_FINALIZE:
	! Called from SY_Installation after all installation related task completed.
	! Menus are compiled here.Test
	! 
RETURN 
!

PRE_UNINSTALL:
	! 
	! This hook is called from Sy_Installation prior to Uninstall
RETURN 
!

POST_UNINSTALL:
! This hook is called from Sy_Installation After Uninstall
!
RETURN 
!

INSTALL_MODULE_FILE:
! 
	PGMCTL			= HFN
	OPEN (PGMCTL,IOL=*,ERR=*RETURN)MODULE_FILE$
	SOACTL			= COSESSION'OPENTABLE("SY_Module","SYSTEM")
	IF NOT(PGMCTL OR SOACTL) { RETURN } 
	SELECT * FROM PGMCTL BEGIN $$ END $FF$
		WRITE (SOACTL)
	NEXT RECORD 
	CLOSE (PGMCTL,ERR=*NEXT)
	CLOSE (SOACTL,ERR=*NEXT)
RETURN 
! 
